prototype(Neos.RedirectHandler.Ui:Module.Export) < prototype(Neos.Fusion:Component) {
    csrfToken = ''

    indexAction = Neos.Fusion:UriBuilder {
        action = 'index'
    }
    exportCsvAction = Neos.Fusion:UriBuilder {
        action = 'exportCsv'
    }
    importAction = Neos.Fusion:UriBuilder {
        action = 'import'
    }

    renderer = afx`
        <div class="neos-content neos-container-fluid">
            <div class="neos-row-fluid">
                <form action={props.exportCsvAction} method="POST" class="export-redirects-form">
                    <input type="hidden" name="__csrfToken" value={String.htmlSpecialChars(props.csrfToken)}/>
                    <div class="neos-control-group neos-control-group--checkbox">
                        <label class="neos-control-label" for="includeInactiveRedirects">
                            <input name="moduleArguments[includeInactiveRedirects]" id="includeInactiveRedirects"
                                   type="checkbox" checked="checked"/>
                            {I18n.translate('Neos.RedirectHandler.Ui:Modules:field.includeInactiveRedirects.label')}
                        </label>
                    </div>
                    <div class="neos-control-group neos-control-group--checkbox">
                        <label class="neos-control-label" for="includeGeneratedRedirects">
                            <input name="moduleArguments[includeGeneratedRedirects]" id="includeGeneratedRedirects"
                                   type="checkbox" checked="checked"/>
                            {I18n.translate('Neos.RedirectHandler.Ui:Modules:field.includeGeneratedRedirects.label')}
                        </label>
                    </div>
                    <div class="neos-control-group neos-control-group--auto">
                        <button type="submit" class="neos-button neos-button-primary">
                            {I18n.translate('Neos.RedirectHandler.Ui:Modules:action.exportCsv')}
                        </button>
                    </div>
                </form>
            </div>

            <div class="neos-footer">
                <a class="neos-button" href={props.indexAction}>
                    {I18n.translate('Neos.RedirectHandler.Ui:Modules:action.backToList')}
                </a>
                <a class="neos-button neos-button-danger" href={props.importAction}>
                    {I18n.translate('Neos.RedirectHandler.Ui:Modules:action.import')}
                </a>
            </div>
        </div>
    `
}
